def stoi fn (string)
  local %stoi = fn (r acc str)
    local val = + (- (first str) '0') (* acc 10)
    if (= 1 (length str))
    then val
    else r val : rest str
  end
  int : (Z2 %stoi) 0 string
end

alias parseInt stoi .

def stor fn (string)
  (splitwhen (char= '/') string)
  ;; Beautiful or cursed? You tell me!
  (fn (h t)
   ratio : stoi h
         : stoi : cdr : t)
end

def rtoi fn (ratio)
  int : div (numer ratio) (denom ratio) .

alias rfloor rtoi .

def itor fn (int)
  ratio int 1 .

;; To string
def itos fn (i)
  local tochar = (+ '0') .
  local %itos = fn (r acc num)
   if (< num 10)
    then cons (tochar num) acc
   else
    r (cons (tochar (mod num 10)) acc) (div num 10)
  end
  str : (Z2 %itos) nil i
end

def rtos fn (r)
  str : intercalate "/" [(itos : numer r) (itos : denom r)] .

def btos fn (b)
  str : if b then "true" else "false" .

def ltos fn (inner list)
  str : rcons ']' : cons '[' : intercalate ", " : map inner list .