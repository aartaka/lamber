def writec fn (c)
  #.(CL:LAMBDA (x) (LAMBER::EVAL (CL:PROGN (CL:PRINC (LAMBER::TOCHAR x)) x))) : char c .

alias putChar writec .

;; Leaking abstraction: map doesn't work without these reverses!
def writes fn (string)
  reverse : reverse : map writec : str string .

alias putStr writes .

def writesln fn (string)
  writes : rcons '\n' : str string  .

alias putStrLn writesln .

def writef fn (name string)
  #.(CL:LAMBDA (NAME)
     (CL:LAMBDA (STR)
      (LAMBER::EVAL
       (UIOP:WITH-OUTPUT-FILE (F (LAMBER::TOSTRING NAME)
                              #.(CL:INTERN "IF-EXISTS" "KEYWORD") (CL:INTERN "SUPERSEDE" "KEYWORD"))
        (CL:WRITE-STRING (LAMBER::TOSTRING STR) F))))) (str name) (str string) .

def writeFile writef .

def readc fn (dummy)
  #.(CL:LAMBDA (_) (CL:DECLARE (CL:IGNORE _)) (LAMBER::EVAL (CL:READ-CHAR))) dummy

alias getChar readc .

def readln fn (dummy)
  local char = readc dummy .
  if (char= '\n' char)
   then nil
  else cons char : readln dummy .

alias getLine readln .

def readf fn (name)
  #.(CL:LAMBDA (x) (LAMBER::EVAL (UIOP:READ-FILE-STRING (LAMBER::TOSTRING x)))) (str name) .

alias readFile readf .